/*
 * libretroshare Android AAR library gradle builder
 *
 * Copyright (C) 2022  Gioacchino Mazzurco <gio@retroshare.cc>
 * Copyright (C) 2022  Asociaci√≥n Civil Altermundi <info@altermundi.net>
 *
 * SPDX-License-Identifier: CC0-1.0
 */

/*
 * Set minumum Android API level supported by this build passing
 * -PANDROID_MIN_API_LEVEL=16 (change 16 to what you need) on the gradle command
 * line.
 *
 * LIBRETROSHARE_SOURCE_VERSION Set source version ex:
 * -PLIBRETROSHARE_SOURCE_VERSION="$(git describe --always)"
 *
 * Optionally set JNI_NATIVE_LIBS_ARCHS to a space separated target CPU
 * architectures list named as seen in AAR native libraries directories passing
 * -PJNI_NATIVE_LIBS_ARCHS="arm64-v8a armeabi-v7a" defaults to all architectures
 * supported by current script at current Android minimum API
 *
 * Optionally set NATIVE_TOOLCHAINS_DIR for easier reuse of toolchains which
 * take a lot to build and doesn't change often
 * -PNATIVE_TOOLCHAINS_DIR="/native/toolchains/path"
 *
 * Optionally set environement variables defined in prepare-toolchain-clang.sh
 * to further customize the build
 */

buildscript
{
	repositories
	{
		// The order in which you list these repositories matter.
		google()
		mavenCentral()
	}

	dependencies
	{
		classpath 'com.android.tools.build:gradle:7.1.+'
	}
}

allprojects
{
	repositories
	{
		// The order in which you list these repositories matter.
		google()
		mavenCentral()
	}
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext.getAndroidMinApiLevel =
{ ->
	if(project.hasProperty('ANDROID_MIN_API_LEVEL'))
	{
		return ANDROID_MIN_API_LEVEL.toInteger();
	}
	else
	{
		return 16
	}
}

ext.getJniNativeLibsArchs =
{ ->
	if(project.hasProperty('JNI_NATIVE_LIBS_ARCHS'))
	{
		return JNI_NATIVE_LIBS_ARCHS.split(' ') as List
	}
	else
	{
		if(getAndroidMinApiLevel() <= 16)
		{
			return ['armeabi-v7a']
		}
		else
		{
			return ['arm64-v8a', 'armeabi-v7a']
		}
	}
}

ext.getLibretroshareSourceVersion =
{ ->
	if(project.hasProperty('LIBRETROSHARE_SOURCE_VERSION'))
	{
		return LIBRETROSHARE_SOURCE_VERSION
	}
	else
	{
		return 'unspecified'
	}
}

ext.getNativeToolchainsDir =
{ ->
	if(project.hasProperty('NATIVE_TOOLCHAINS_DIR'))
	{
		return NATIVE_TOOLCHAINS_DIR
	}
	else
	{
		return "${buildDir}/native_toolchains/"
	}
}

ext.getjniLibsDir =
{ ->
	return "${buildDir}/native_libs/"
}

ext.getAssetsDir =
{ ->
	return "${buildDir}/android-assets/"
}

ext.getArtifactBaseName =
{  ->
	return project.name + "-MinApiLevel" + getAndroidMinApiLevel()
}

ext.buildLibretroshareNativeLib =
{
	pApiLevel, /* Android API level */
	pAbi, /* Arch name as seen in AAR native libraries directories */
	pNdkPath, /* Android NDK path */
	pReuseToolchain = true /* If true reuse previously built toochain */ ->

	/* Convert pAbi into corresponding prepare toolchain ANDROID_NDK_ARCH */
	def toolchainArch = "unsupported"
	def libcxxsharedTriple = "unsupported"
	switch(pAbi)
	{
	case "armeabi-v7a":
		toolchainArch = "arm";
		libcxxsharedTriple = "arm-linux-androideabi";
		break;
	case "arm64-v8a":
		toolchainArch = "arm64";
		libcxxsharedTriple = "aarch64-linux-android";
		break;
	default:
		throw new GradleException(
			"buildLibretroshareNativeLib unsupported pAbi: $pAbi" );
		break;
	}

	def toolchainsWorkdir = getNativeToolchainsDir()
	mkdir toolchainsWorkdir

	def currToolchainPath = "$toolchainsWorkdir/$pApiLevel-$toolchainArch/"

	// Todo: use proper way to resolve the script path
	def toolChainScriptPath = "${projectDir}/misc/Android/prepare-toolchain-clang.sh"

	if(!pReuseToolchain || !file(currToolchainPath).exists())
	{
		exec
		{
			workingDir toolchainsWorkdir
			environment "ANDROID_NDK_PATH", pNdkPath
			environment "NATIVE_LIBS_TOOLCHAIN_PATH", currToolchainPath
			environment "ANDROID_PLATFORM_VER", pApiLevel
			environment "ANDROID_NDK_ARCH", toolchainArch
			commandLine toolChainScriptPath
		}
	}
	else
	{
		exec
		{
			workingDir toolchainsWorkdir
			environment "ANDROID_NDK_PATH", pNdkPath
			environment "NATIVE_LIBS_TOOLCHAIN_PATH", currToolchainPath
			environment "ANDROID_PLATFORM_VER", pApiLevel
			environment "ANDROID_NDK_ARCH", toolchainArch
			commandLine toolChainScriptPath, 'build_libretroshare'
		}
	}

	mkdir getjniLibsDir()

	def currAbiLibDir = "${getjniLibsDir()}/$pAbi/"
	mkdir currAbiLibDir
	copy
	{
		from "${currToolchainPath}/sysroot/usr/lib/libretroshare.so"
		into currAbiLibDir
	}

	copy
	{
		from "${currToolchainPath}/sysroot/usr/lib/${libcxxsharedTriple}/libc++_shared.so"
		into currAbiLibDir
	}

	copy
	{
		from "${currToolchainPath}/sysroot/usr/lib/${libcxxsharedTriple}/${pApiLevel}/liblog.so"
		into currAbiLibDir
	}

	def bdbootAssetDir = "${getAssetsDir()}/values"
	mkdir bdbootAssetDir
	copy
	{
		from "${currToolchainPath}/sysroot/usr/share/retroshare/bdboot.txt"
		into bdbootAssetDir
	}
}

task prepareRetroshareNativeLibs
{
	doLast
	{
		def pNdkPath = android.getNdkDirectory().getAbsolutePath()
		getJniNativeLibsArchs().each
		{
			abi ->
			buildLibretroshareNativeLib(getAndroidMinApiLevel(), abi, pNdkPath)
		}
	}
}
preBuild.dependsOn prepareRetroshareNativeLibs

android
{
	// see https://stackoverflow.com/questions/27301867/what-is-compilesdkversion
	compileSdkVersion 21

	ndkVersion "21.0.6113669"

	defaultConfig
	{
		minSdkVersion getAndroidMinApiLevel()
		targetSdkVersion 28
	
		/* Prevent proguard run at applications build time, to alter
		 * libretroshare Java classes that are used by libretroshare C++ code */
		consumerProguardFiles 'misc/Android/proguard-keep-libretroshare-java-classes.pro'
	}

	setProperty("archivesBaseName", getArtifactBaseName())

	sourceSets
	{
		main
		{
			java.srcDirs = [ 'src/rs_android/' ]
			manifest.srcFile 'src/rs_android/AndroidManifest.xml'
			assets.srcDirs = [ getAssetsDir() ]
			jniLibs.srcDirs = [ getjniLibsDir() ]
		}
	}

	lintOptions
	{
		 disable 'LongLogTag'
	}

	 publishing
	 {
		multipleVariants
		{
			allVariants()
		}
	}
}

afterEvaluate
{
	publishing
	{
		// see https://developer.android.com/reference/tools/gradle-api/7.1/com/android/build/api/dsl/LibraryPublishing
		publications
		{
			debug(MavenPublication)
			{
				groupId = 'org.retroshare.service'
				artifactId "${getArtifactBaseName()}-debug"
				version getLibretroshareSourceVersion()
				artifact bundleReleaseAar
			}
			release(MavenPublication)
			{
				groupId = 'org.retroshare.service'
				artifactId "${getArtifactBaseName()}-release"
				version getLibretroshareSourceVersion()
				artifact bundleReleaseAar
			}
		}
		repositories
		{
			maven
			{
				url =  uri("https://gitlab.com/api/v4/projects/${System.getenv('CI_PROJECT_ID')}/packages/maven")
				name = "Gitlab"
				credentials(HttpHeaderCredentials)
				{
					name = "Job-Token"
					value = System.getenv("CI_JOB_TOKEN")
				}
				authentication
				{
					header(HttpHeaderAuthentication)
				}
			}
		}
	}
}
